
import org.jbpm.document.Document;
import com.redhat.demos.bpms_kyc_demo.DocumentsOK;
import com.redhat.demos.bpms_kyc_demo.Customer;

//##################### Income Document rules #####################

rule "Clean DocumentsOK income from session."
ruleflow-group "income-documents-check"
salience 100
lock-on-active
when
    $d:DocumentsOK(type == "income")
then
    delete($d);
end 


rule "[1] Passport Document Check - Document starts with surname of customer"
ruleflow-group "income-documents-check"
when
    Customer($s:surname)
    $d:Document(name str[startsWith] $s)
then
    System.out.println("Passport OK");
    //Reset validation message
    $d.addAttribure("validationMessage", null);
    //Insert marker that passport is OK.
    insertLogical(new DocumentsOK("passport"));
end

rule "[1] Passport Document Check - Document starts with surname of customer - Validation Message"
ruleflow-group "income-documents-check"
when
    Customer($s:surname)
    $d:Document(name not str[startsWith] $s)
then
    System.out.println("Passport OK");
    $d.addAttribute("validationMessage", "Document name does not start with surname of customer.");
end


//We can't user insertLogical here, because the RFG node will retract the facts from WM when it's finished.
//This means that all matches are removed and thus the "DocumentsOK("incomce"))" fact will be removed from WM
// as well. This causes the gateway not to work because the gateway checks the presence of a fact in WM.
rule "AllIncomeDocumentsOK"
ruleflow-group "income-documents-check"
when
    DocumentsOK(type == "passport")
then
    System.out.println("All income documents OK.");
    insert(new DocumentsOK("income"));
end


//##################### Milestone Document rules #####################


rule "Documents OK"
ruleflow-group "documents-check"
when
    DocumentsOK(type == "income")
then
    System.out.println("Documents OK");
    insert (new DocumentsOK("kyc"));
end
    
